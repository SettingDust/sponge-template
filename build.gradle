buildscript {
    repositories {
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10")
        classpath("com.github.jengelman.gradle.plugins:shadow:6.0.0")
        classpath("org.jmailen.gradle:kotlinter-gradle:3.0.2")
    }
}

plugins {
    id("net.kyori.blossom")
}

applyPlugin("org.jetbrains.kotlin.jvm")
applyPlugin("org.jetbrains.kotlin.kapt")
applyPlugin("com.github.johnrengelman.shadow")
applyPlugin("org.jmailen.kotlinter")

def applyPlugin(id) {
    if (project.plugins.findPlugin(id) != null) {
        println("$id already applied")
    } else {
        apply plugin: id
    }
}

def major = 1
def minor = 0
def patch = 2

def mainVersion = [major, minor, patch].join(".")

group = "org.example"
version = """$mainVersion${
    new ArrayList() {
        {
            add("")

            if (System.getenv("BUILD_NUMBER") != null)
                add(System.getenv("BUILD_NUMBER").toString())

            if (System.getenv("GITHUB_REF") == null || System.getenv("GITHUB_REF").endsWith("-dev"))
                add("unstable")
        }
    }.join("-")}"""

repositories {
    mavenCentral()
    maven {
        url = "https://repo.spongepowered.org/maven/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    def kotlin = "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation kotlin
    shadow kotlin

    def spongeApi = "org.spongepowered:spongeapi:7.3.0"
    implementation spongeApi
    kapt spongeApi

    def bstats = "org.bstats:bstats-sponge:1.7"
    implementation bstats
    shadow bstats
    kapt bstats

    def lavenSponge = project("laven-sponge")
    api lavenSponge
    shadow(lavenSponge) {
        exclude group: "org.spongepowered", module: "spongeapi"
        exclude group: "org.jetbrains.kotlin"
    }

    api project("laven-sponge:laven")
}

blossom {
    replaceToken "@version@", mainVersion
}

kotlinter {
    ignoreFailures = true
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
jar {
    actions = []
    finalizedBy shadowJar
}
shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.shadow]
    exclude "META-INF/**", "org/intellij/lang/annotations/**", "org/jetbrains/annotations/**"

    relocate "ninja.leaping.configurate.kotlin", "${project.getGroup()}.runtime.configurate.kotlin"
    relocate "kotlin", "${project.getGroup()}.runtime.kotlin"
    relocate "org.bstats", "${project.getGroup()}.runtime.bstats"
    relocate "me.settingdust.laven", "${project.getGroup()}.runtime.laven"
}
build { dependsOn("formatKotlinMain") }